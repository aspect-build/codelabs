"Declare dependencies for bzlmod, see https://bazel.build/build/bzlmod"

bazel_dep(name = "apple_support", version = "1.11.1")
bazel_dep(name = "aspect_bazel_lib", version = "1.35.0")
bazel_dep(name = "aspect_rules_js", version = "1.32.6")
bazel_dep(name = "aspect_rules_ts", version = "2.0.0")
bazel_dep(name = "aspect_rules_swc", version = "1.1.0")
bazel_dep(name = "bazel_skylib", version = "1.4.2")
bazel_dep(name = "protobuf", version = "21.7")
bazel_dep(name = "gazelle", version = "0.32.0")
bazel_dep(name = "rules_apple", version = "3.0.0")
bazel_dep(name = "rules_go", version = "0.41.0")
bazel_dep(name = "rules_jvm_external", version = "4.5")
bazel_dep(name = "rules_python", version = "0.27.1")
bazel_dep(name = "rules_swift", version = "1.12.0")
bazel_dep(name = "rules_swift_package_manager", version = "0.12.0")
bazel_dep(name = "rules_xcodeproj", version = "1.11.0")

# Python
# https://github.com/bazelbuild/rules_python/tree/main/examples/bzlmod
pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    python_version = "3.9",
)

pip.parse(
    hub_name = "pip",
    requirements_lock = "//:requirements_lock.txt",
    python_version = "3.9",
)
use_repo(pip, "pip")

# Java and other JVM languages:
# https://github.com/bazelbuild/rules_jvm_external/blob/master/examples/bzlmod/MODULE.bazel
# https://github.com/bazelbuild/rules_jvm_external#pinning-artifacts-and-integration-with-bazels-downloader
maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    artifacts = ["io.grpc:grpc-all:1.51.1"],
    lock_file = "//:maven_install.json",
)
use_repo(
    maven,
    "maven",
    "unpinned_maven",
)

# JavaScript and TypeScript
# https://github.com/aspect-build/rules_js/tree/main/e2e/bzlmod
# https://github.com/aspect-build/rules_ts/tree/main/e2e/bzlmod
pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm")
# Allows developers to get a local node_modules tree, using
# bazel run -- @pnpm//:pnpm --dir $PWD install
use_repo(pnpm, "pnpm")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "npm",
    npmrc = "//:.npmrc",
    pnpm_lock = "//:pnpm-lock.yaml",
)
use_repo(npm, "npm")

rules_ts_ext = use_extension(
    "@aspect_rules_ts//ts:extensions.bzl",
    "ext",
    dev_dependency = True,
)
rules_ts_ext.deps()
use_repo(rules_ts_ext, "npm_typescript")

# Go
# See https://github.com/bazelbuild/rules_go/blob/master/docs/go/core/bzlmod.md
go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(
    name = "go_sdk",
    version = "1.21.1",
)
use_repo(go_sdk, "go_sdk")

go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//:go.mod")
use_repo(go_deps, "org_golang_google_grpc", "org_golang_google_protobuf")

non_module_deps = use_extension("@rules_swift//swift:extensions.bzl", "non_module_deps")
use_repo(
    non_module_deps,
    "build_bazel_rules_swift_index_import",
    "build_bazel_rules_swift_local_config",
    "com_github_apple_swift_log",
    "com_github_apple_swift_nio",
    "com_github_apple_swift_nio_extras",
    "com_github_apple_swift_nio_http2",
    "com_github_apple_swift_nio_transport_services",
    "com_github_apple_swift_protobuf",
    "com_github_grpc_grpc_swift",
)

# swift_deps START
swift_deps = use_extension(
    "@rules_swift_package_manager//:extensions.bzl",
    "swift_deps",
)
swift_deps.from_file(
    deps_index = "//:swift_deps_index.json",
)
use_repo(
    swift_deps,
    "swiftpkg_swifterswift",
)
# swift_deps END
